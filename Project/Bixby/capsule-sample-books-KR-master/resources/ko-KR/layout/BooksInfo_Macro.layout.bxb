// 참조(https://bixbydevelopers.com/dev/docs/dev-guide/developers/building-views.layout-macros)
// 참조(https://bixbydevelopers.com/dev/docs/reference/type/layout-macro-def.content)
layout-macro-def (BooksInfo) {
  params {
    param (info) {
      type (Books)
      min (Required) max (Many)
    }
  }
  content {
    section{
      content{  
        for-each(info.book){ // 객체를 이터레이트하기 위한 방법, 사용법: for-each(upper.targetObject){ as (targetObject) { grammar }} 
          as (book){
            thumbnail-card{
              title-area{
                halign(Start)
                slot1{
                  text{
                    style(Title_S)
                    value("[#{value(book.volume.title)}]")
                  }
                }
                slot2{
                  single-line {
                    text{
                      style(Title_XS)
                      value("[저자: #{value(book.volume.authors)}]")
                    }
                  }          
                }
                slot3{
                  single-line{
                    text{
                      style(Detail_M_Soft)
                      value("[#{value(book.volume.description)}]")
                    }
                  }
                }
              }
              image-url("[#{value(book.volume.imageLinks)}]")
              image-position (Start)
              on-click{
                intent {
                  goal: GetBookDetailByURL
                  value: $expr(book.detailLink)
                }
              }
            }
          }
        }
        
        // app-launch을 실행하는 버튼 구현
        cell-card{
          slot1{
            image{
              url("icon/new-google-favicon-512.png")
              shape (Circle)
            }
          }
          slot2{
            content{
              primary("구글 북서치에서 더보기")
            }
          }
          on-click{
            intent {
              goal: OpenBrowser
              value: $expr(info.query)
            }
          }
        }
      }
    }
  }
}

