// 참조(https://bixbydevelopers.com/dev/docs/dev-guide/developers/building-views.layout-macros)
// 참조(https://bixbydevelopers.com/dev/docs/reference/type/layout-macro-def.content)
layout-macro-def (BookInfo) {
  params {
    param (info) {
      type (Book)
      min (Required) max (One)
    }
  }
  content {
    section{
      content{
        compound-card {
          content {
            image-card {
              aspect-ratio (4:3)
              image-url ("[#{value(info.volume.imageLinks)}]")
              image-object-fit (Contain)
              title-area {
                halign (Start)
                slot1{
                  text{
                    style(Title_S)
                    value("")
                  }
                }               
              }
            }
            single-line {
              text {
                value {
                  template ("[#{value(info.volume.title)}]")
                }
                style (Title_M)
              }
            }
            paragraph {
              value {
                template ("[저자: #{value(info.volume.authors)}]")
              }
              style (Detail_L)
            }
            single-line {
              text {
                value {
                  template ("[출판사: #{value(info.volume.publisher)}]")
                }
                style (Detail_M_Soft)
              }
            }
          }
        }
        if (exists(info.sales.listPrice.currencyCode) || exists(info.sales.retailPrice.currencyCode)){
          cell-card{
            slot2{
              content{
                order (PrimarySecondary)
                primary("할인가: [#{value(info.sales.retailPrice.amount)} #{value(info.sales.retailPrice.currencyCode)}]")
                secondary("정가: [#{value(info.sales.listPrice.amount)} #{value(info.sales.listPrice.currencyCode)}]")
              }
            }
          }
        }
        if (exists(info.volume.categories)){
          cell-card{
            slot2{
              content{
                order (PrimarySecondary)
                primary("카테고리: #{value(info.volume.categories)}")
              }
            }
          }
        }
        cell-card{
          slot2{
            content{
              order (PrimarySecondary)
              primary("[평점: #{value(info.volume.averageRating)} / 평점자 수: #{value(info.volume.ratingsCount)}]")
              secondary("[출판일: #{value(info.volume.publishedDate)}] [페이지 수: #{value(info.volume.pageCount)}]")
            }
          }
        }
        if (exists(info.volume.description)){
          paragraph{
            value{
              template ("책 소개:\n #{value(info.volume.description)}")
            }
            style(Detail_L)
          }
        }
        
        if (info.sales.saleability == true){
          // app-launch을 실행하는 버튼 구현
          cell-card{
            slot1{
              image{
                url("icon/new-google-favicon-512.png")
                shape (Circle)
              }
            }
            slot2{
              content{
                primary("구글 Play Store에서 구매하기")
              }
            }
            on-click{
              intent {
                goal: OpenBrowser
                value: $expr(info)
              }
            }
          }
        }        
      }
    }
  }
}

